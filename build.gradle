buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

if (file('local.gradle').exists()) apply from: 'local.gradle'

allprojects {
    repositories {
        jcenter()
    }
}

subprojects {
    ext.compileSdkVersion = 23
    ext.buildToolsVersion = '23.0.3'
    ext.supportLibrariesVersion = '23.2.1'
}

configure(subprojects.findAll { it.name.startsWith("decorators-") }) {
    apply plugin:'com.android.application'

    android {
        defaultConfig.manifestPlaceholders = [ permission: "com.oasisfeng.nevo.permission.BIND_DECORATOR_SERVICE" ]

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }

        lintOptions {
            abortOnError false
        }
    }

    dependencies {
        debugCompile project(':sdk')
        releaseCompile 'com.oasisfeng.nevo:sdk:0.9.1'
    }
}
